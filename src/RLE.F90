!> @file
!>  Contains a single module that
!>  provides data compression (run-length encoding) for internal data structures.
module RLE

  use iso_c_binding, only : c_short, c_int, c_float, c_double
  use types

  implicit none

contains

!--------------------------------------------------------------------------
!!****s* RLE/RLE_writeByte
! NAME
!   RLE_writeByte - Apply run-length encoding to a bytestream and write out.
!
! SYNOPSIS
!   Converts a real value to an integer, and applies
!   a run-length encoding scheme to compress output data.
!
! INPUTS
!   iLU - FORTRAN logical unit to which output should be written
!   rValue - real value from the datastream to be compressed.
!   iMultiplicationFactor - integer multiplication factor used to
!      convert the real to an integer.  Correlates to the number of
!      decimal places to preserve in the datastream.
!   lEOF - logical value - .TRUE. only if the datastream transmission has
!          ended.
!
! OUTPUTS
!   None.
!
! SOURCE

!>  Converts a real value to an integer, and applies
!>  a run-length encoding scheme to compress output data.
subroutine RLE_writeByte(iLU, rValue, iRLE_MULT, rRLE_OFFSET, &
                    iByteTotal, iVarNum)

  ![ARGUMENTS]
  integer(c_int), intent(in) :: iLU
  real(c_float),intent(in) :: rValue
  integer(c_int),intent(in) :: iRLE_MULT
  real(c_float),intent(in) :: rRLE_OFFSET
  integer(c_int), intent(in) :: iByteTotal      ! total # of bytes in
                                                     ! original datastream
  integer(c_int), intent(in) :: iVarNum

  ![LOCALS]
  integer(c_int),dimension(1:iNUM_VARIABLES), save :: iRunCount
  integer(c_int),dimension(1:iNUM_VARIABLES), save :: iByteCount = 0
                                                ! total # of bytes fed into
                                                ! RLE_writeByte so far
  integer(c_int) :: i
  integer(c_int),dimension(1:iNUM_VARIABLES), save :: iPrevious, iCurr
  logical(c_bool),dimension(1:iNUM_VARIABLES),save :: lRun

  ! iByteCount is incremented *EVERY* time this subroutine is called.
  ! This subroutine should be called a number of times that EXACTLY
  ! matches the number of items in the array to be compressed
  iByteCount(iVarNum) = iByteCount(iVarNum) + 1

  if(iByteCount(iVarNum) == 1) then

    iCurr(iVarNum) = NINT((rValue + rRLE_OFFSET) * iRLE_MULT)
	  iRunCount(iVarNum) = 0
	  lRun(iVarNum) = lFALSE

  else ! 1 < iByteCount <= iByteTotal

    ! iCurr from last iteration becomes iPrevious in this iteration

    iPrevious(iVarNum) = iCurr(iVarNum)

    iCurr(iVarNum) = NINT((rValue + rRLE_OFFSET) * iRLE_MULT)

    if(iPrevious(iVarNum) == iCurr(iVarNum)) then ! we have a new or existing run

      if(lRun(iVarNum)) then  ! EXISTING RUN
	      lRun(iVarNum) = lTRUE
        iRunCount(iVarNum) = iRunCount(iVarNum) + 1
      else  ! BEGIN A NEW RUN
        write(iLU) iPrevious(iVarNum)
        write(iLU) iCurr(iVarNum)
        lRun(iVarNum) = lTRUE
        iRunCount(iVarNum) = 0
      end if

	  elseif(iPrevious(iVarNum)/=iCurr(iVarNum)) then
      
      if(lRun(iVarNum)) then  ! *END* EXISTING RUN
        write(iLU) iRunCount(iVarNum)
		    lRun(iVarNum) = lFALSE
		    iRunCount(iVarNum) = 0
      else  ! OK to write out iPrevious
        write(iLU) iPrevious(iVarNum)
        lRun(iVarNum) = lFALSE
        iRunCount(iVarNum) = 0
      end if

	  endif

  endif


!#ifdef DEBUG_PRINT
!    write(UNIT=LU_LOG,FMT=*) iByteCount(iVarNum),"  rValue: ",real2char(rValue), &
!      ":  iPrev: ",iPrevious(iVarNum),"  iCurr: ", iCurr(iVarNum), "  lRun: ", &
!      lRun(iVarNum), "  iRunCount: ", iRunCount(iVarNum)
!#endif

  if(iByteCount(iVarNum) == iByteTotal) then  ! last byte of information... clean up and exit

      if(lRun(iVarNum)) then  ! EOF in middle of EXISTING RUN
      write(iLU) iRunCount(iVarNum)    ! - 1
#ifdef DEBUG_PRINT
	  write(UNIT=LU_LOG,FMT=*) "iByteCount == iByteTotal: EOF in EXISTING RUN"
#endif
	else
      write(iLU) iCurr(iVarNum)
#ifdef DEBUG_PRINT
  	  write(UNIT=LU_LOG,FMT=*) "iByteCount == iByteTotal: EOF: no existing run"
#endif
    end if

	! write EOF flag to binary file
    write(iLU) iEOF
    FLUSH(iLU)

	! reset byte count
	iByteCount(iVarNum) = 0

  endif

!#ifdef DEBUG_PRINT
!  write(UNIT=LU_LOG,FMT=*) iByteCount,":  iPrev: ",iPrevious,"  iCurr: ", iCurr, "  lRun: ", &
!     lRun, "  iRunCount: ", iRunCount
!#endif

  return

end subroutine RLE_writeByte

!!***
!--------------------------------------------------------------------------
!!****s* RLE/RLE_readByte
! NAME
!   RLE_readByte - Read and apply run-length encoding to a bytestream.
!
! SYNOPSIS
!   Converts a real value to an integer, and applies
!   a run-length encoding scheme to compress output data.
!
! INPUTS
!   iLU - FORTRAN logical unit to which output should be written
!   rValue - real value from the datastream to be compressed.
!   iMultiplicationFactor - integer multiplication factor used to
!      convert the real to an integer.  Correlates to the number of
!      decimal places to preserve in the datastream.
!   lEOF - logical value - .TRUE. only if the datastream transmission has
!          ended.
!
! OUTPUTS
!   None.
!
! SOURCE

!>  Converts an integer value to a real value by decompressing a data stream
!>  by means of a run-length encoding scheme.
subroutine RLE_readByte(iLU,iRLE_MULT, rRLE_OFFSET, rValue, &
                        iByteTotal,lEOF)

  ![ARGUMENTS]
  integer(c_int), intent(in) :: iLU
  integer(c_int),intent(in) :: iRLE_MULT
  real(c_float),intent(in) :: rRLE_OFFSET
  real(c_float),dimension(iByteTotal), intent(out) :: rValue
  integer(c_int), intent(in) :: iByteTotal      ! total # of bytes in
                                                           ! original datastream
  logical(c_bool),intent(out) :: lEOF

  ![LOCALS]
  integer(c_int), save :: iByteCount   ! total # of bytes reconstituted
                                                  ! by RLE_readByte so far

  integer(c_int) iCurr, i, iRepeat, iStat
  integer(c_int), save :: iPrevious
  logical(c_bool),save :: lRun
  lEOF = lFALSE


  ! begin reconstituting the array by assigning an initial value to
  ! iCurr
  read(iLU,iostat=iStat) iCurr  ! read in a byte of data
  call Assert(iStat == 0, &
     "Error reading input file: module RLE, subroutine RLE_readByte")
  lRun = lFALSE
  iByteCount = 0

  LOOP: do

    ! iCurr from last iteration becomes iPrevious in this iteration
    iPrevious = iCurr

    read(iLU,iostat=iStat) iCurr  ! read in a byte of data
    call Assert(iStat == 0, &
       "Error reading input file: module RLE, subroutine RLE_readByte")
    call Assert(LOGICAL(iCurr>=-HUGE(iCurr),c_bool), &
        "Integer overflow; iLU= "//TRIM(int2char(iLU)) &
        //"; iCurr= "//TRIM(int2char(iCurr)) &
          //". Use a smaller value for RLE_MULTIPLIER", &
                trim(__FILE__),__LINE__)

	if(iPrevious /= iCurr) then  ! set rValue equal to the value of iPrevious
	  iByteCount = iByteCount + 1

	  lRun = lFALSE

	  call Assert(LOGICAL( iByteCount <=iByteTotal, c_bool), &
        "iByteCount exceeds iByteTotal: module RLE, subroutine RLE_readByte")

      rValue(iByteCount) = REAL(iPrevious, c_float) &
                           / REAL(iRLE_MULT, c_float) &
                           - rRLE_OFFSET

!      write(UNIT=LU_LOG,FMT=*) iByteCount,":  iPrev: ",iPrevious,"  iCurr: ", iCurr, "  rValue: ", &
!         rValue(iByteCount)

    else if(iCurr==iPrevious) then  ! we have a run

	  lRun = lTRUE

	  ! assign value of iCurr
      iByteCount = iByteCount + 1
      call Assert(LOGICAL( iByteCount <=iByteTotal, c_bool), &
        "iByteCount exceeds iByteTotal", &
                trim(__FILE__),__LINE__)

!      rValue(iByteCount) = REAL(iCurr, c_float) / REAL(iRLE_MULT, c_float)

      rValue(iByteCount) = REAL(iCurr, c_float) &
                           / REAL(iRLE_MULT, c_float) &
                           - rRLE_OFFSET

      call Assert(LOGICAL(iCurr>=-iEOF,c_bool), &
        "Read beyond end of file marker", &
                trim(__FILE__),__LINE__)

!      write(UNIT=LU_LOG,FMT=*) iByteCount,":  iPrev: ",iPrevious,"  iCurr: ", iCurr, "  rValue: ", &
!        rValue(iByteCount)


	  ! now read in the number of times to repeat iCurr
      read(iLU,iostat=iStat) iRepeat

	  ! if iRepeat is ZERO, loop will *NOT* execute
      do i=iRepeat,1,-1
        iByteCount = iByteCount + 1
	    call Assert(LOGICAL( iByteCount <=iByteTotal, c_bool), &
          "iByteCount exceeds iByteTotal: module RLE, subroutine RLE_readByte", &
                trim(__FILE__),__LINE__)
!        rValue(iByteCount) = REAL(iCurr, c_float) / REAL(iRLE_MULT, c_float)

        rValue(iByteCount) = REAL(iCurr, c_float) &
                           / REAL(iRLE_MULT, c_float) &
                           - rRLE_OFFSET

        call Assert( iCurr >= -iEOF, &
         "Integer overflow; use a smaller value for RLE_MULTIPLIER", &
         TRIM(__FILE__), __LINE__)

!    	write(UNIT=LU_LOG,FMT=*) iByteCount,":  iPrev: ",iPrevious,"  iCurr: ", iCurr, "  rValue: ", &
!          rValue(iByteCount)

      end do

	else

	  call Assert(lFALSE,"Uncaught condition in IF-THEN block", &
	    TRIM(__FILE__), __LINE__)

	end if

    if(iByteCount == iByteTotal) then  ! we have already assigned the last
	                                   ! value to rValue....check for EOF

      call Assert( iCurr == iEOF, &
        "Expected to find an EOF marker in binary file", &
        TRIM(__FILE__), __LINE__)

	  exit LOOP

	end if

  end do LOOP

  return

end subroutine RLE_readByte

!!***

!-----------------------------------------------------------------------

end module RLE
